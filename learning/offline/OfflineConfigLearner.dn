uses offline.learning_types.Policy
data RequestPatternRange {
    dec imgVolMin
    dec imgVolMax
    dec textVolMin
    dec textVolMax
    dec reqCountMin
    dec reqCountMax
}

data ConfigResponseTimeTuple {
    int config
    dec rt
}

component provides offline.OfflineConfigLearner requires util.Math ma, io.TextFile, util.Random ru, time.Calendar calendar, time.DateUtil dateUtil, io.File, net.TCPSocket, data.adt.List, data.StringUtil su, data.adt.HashTable, clustering.ChangePointClustering cpc, ml.rl.RL, io.Output out, pal.control.RestAPI, net.http.HTTPRequest request, time.Timer timer, data.IntUtil iu, data.DecUtil du, data.json.JSONParser parser, io.FileSystem fs {
    const char DEFAULT_SERVER_IP[] = "127.0.0.1"
    const int DEFAULT_SERVER_PORT = 8009
    const int DEFAULT_WEB_PORT = 8090
    const int RECV_BUF_SIZE = 512

    int learningComplete

    char serverRootDir[] = "/home/ben/Documents/PhD/rex_ws/web_server/htdocs/"
    List serverImageEndpoints = new List()
    List serverTextEndpoints = new List()

    List serverImageEndpointData = new List()
    List serverTextEndpointData = new List()

    char[] OfflineConfigLearner:getEndpointFileTypeString(OfflineServerEndpoint ep) {
        return iu.makeString(ep.fileType)
    }

    char[] OfflineConfigLearner:getEndpointFileSizeString(OfflineServerEndpoint ep) {
        return iu.makeString(ep.fileSize)
    }

    dec OfflineConfigLearner:returnSomething() {
        return 1.0
    }

    OfflineServerEndpoint findClosestSize(List serverEndpointData, dec toFind) {
        for (int i = 0; i < serverEndpointData.getLength(); i++) {
            OfflineServerEndpoint x = serverEndpointData.getIndex(i)
        }
        int listLen = serverEndpointData.getLength()
        dec d_listLen = listLen
        if (listLen == 1) {
            OfflineServerEndpoint x = serverEndpointData.getIndex(0)
            return x
        }
        int pivot = ma.floor(d_listLen/2)
        OfflineServerEndpoint x = serverEndpointData.getIndex(pivot)
        List lower = new List()
        for (int i = 0; i < pivot; i++) {
            OfflineServerEndpoint y = serverEndpointData.getIndex(i)
            lower.add(y)
        }
        List higher = new List()
        for (int i = pivot; i < listLen; i++) {
            OfflineServerEndpoint y = serverEndpointData.getIndex(i)
            higher.add(y)
        }
        OfflineServerEndpoint pivotCompare = lower.getIndex(lower.getLength()-1)
        if ( (toFind - x.fileSize) < (toFind - pivotCompare.fileSize)) {
            return findClosestSize(higher, toFind)
        }
        else {
            return findClosestSize(lower, toFind)
        }
    }

    //sorts the serverEndpointData lists by size 
    List sortEndpointData(List serverEndpointData) {
        int len = serverEndpointData.getLength() 
        if (len == 0) {return null}
        else if (len == 1) {return serverEndpointData}

        int lastElIndex = len-1
        OfflineServerEndpoint pivotObj = serverEndpointData.getIndex(lastElIndex)
        dec pivot = pivotObj.fileSize

        List lower = new List()
        List higher = new List()

        for (int i = 0; i < lastElIndex; i++) {
            OfflineServerEndpoint x = serverEndpointData.getIndex(i)
            if (x.fileSize < pivot) {
                lower.add(x)
            }
            else {
                higher.add(x)
            }
        }

        lower = sortEndpointData(lower)
        higher = sortEndpointData(higher)

        List sorted = new List()
        //concat lower and higher with pivot in the middle
        if (lower != null) {
            for (int i = 0; i < lower.getLength(); i++) {
                OfflineServerEndpoint x = lower.getIndex(i)
                sorted.add(x)
            }
        }
        sorted.add(pivotObj)
        if (higher != null) {
            for (int i = 0; i < higher.getLength(); i++) {
                OfflineServerEndpoint x = higher.getIndex(i)
                sorted.add(x)
            }
        }

        return sorted
    }

    void OfflineConfigLearner:initilise() {
        ru.setSeed(dateUtil.toMilliseconds(calendar.getTime()))
        learningComplete = 0
        //open dir that contains resources for webserver
        FileEntry serverResources[] = fs.getDirectoryContents(serverRootDir)
        //add all entries ending .jpg or .html to serverEndpoints
        for (int i = 0; i < serverResources.arrayLength; i++) {
            if (su.endsWith(serverResources[i].name, ".jpg")) {
                serverImageEndpoints.add(new String(serverResources[i].name))
            }
            else if (su.endsWith(serverResources[i].name, ".html")) {
                serverTextEndpoints.add(new String(serverResources[i].name))
            }
        }
        //open up a connection to the webserver and request each endpoint storing the information
        //about the endpoint, size, filetype etc
        for (int i = 0; i < serverImageEndpoints.getLength(); i++) {
            String x = serverImageEndpoints.getIndex(i)
            char req[] = new char[]("GET ", "/", x.string, " HTTP/1.1\r\n\r\n")

            TCPSocket s = new TCPSocket()
            s.connect(DEFAULT_SERVER_IP, DEFAULT_WEB_PORT)
            
            s.send(req)
            int totalReceived = 0
            byte resp[]
            while ((resp = s.recv(RECV_BUF_SIZE)).arrayLength > 0) {
                totalReceived += resp.arrayLength
            }

            serverImageEndpointData.add(new OfflineServerEndpoint(x, IMAGE, totalReceived))
            
            s.disconnect()
        }
        for (int i = 0; i < serverTextEndpoints.getLength(); i++) {
            String x = serverTextEndpoints.getIndex(i)
            char req[] = new char[]("GET ", "/", x.string, " HTTP/1.1\r\n\r\n")

            TCPSocket s = new TCPSocket()
            s.connect(DEFAULT_SERVER_IP, DEFAULT_WEB_PORT)
           
            s.send(req)
            int totalReceived = 0
            byte resp[]
            while ((resp = s.recv(RECV_BUF_SIZE)).arrayLength > 0) {
                totalReceived += resp.arrayLength
            }

            serverTextEndpointData.add(new OfflineServerEndpoint(x, TEXT, totalReceived))
            
            s.disconnect()
        }
        serverTextEndpointData = sortEndpointData(serverTextEndpointData)
        serverImageEndpointData = sortEndpointData(serverImageEndpointData)
        return
    }

    //Author: Roberto Filho	
	dec scaleCost(dec cost, dec lowCost, dec highCost)
		{
		//here we lock the cost between the given range, where anything falling outside of that range is just set of highCost
		// - we then do 1 - cost to convert to reward
		
		//first truncate at high/low cost
		if (cost > highCost)
			{
			cost = highCost
			}
			else if (cost < lowCost)
			{
			cost = lowCost
			}
		
		//shift negative low
		if (lowCost < 0.0)
			{
			dec mod = lowCost / -1.0
			cost += mod
			lowCost += mod
			highCost += mod
			}
		
		//now convert the range into 0.0 -> 1.0
		dec scaledCost = cost / highCost
		
		return scaledCost
		}

    //Author: Roberto Filho	
	dec costToReward(dec cost)
		{
		//invert to get reward
		return 1.0 - cost
		}

    DateTime dateFromStr(char str[]) {
        //ignore first 11 chars (year)
        char time[] = new char[10] //change to 11 if need null term
        for (int i = 11; i < str.arrayLength; i++) {
            time[i-11] = str[i]
        }
        String tokens[] = su.explode(time, ":")
        DateTime start = new DateTime()
        int hour = iu.intFromString(tokens[0].string)
        start.hour = hour
        int min = iu.intFromString(tokens[1].string)
        start.minute = min
        int sec = iu.intFromString(tokens[2].string)
        start.second = sec
        return start
    }

    ConfigResponseTimeTuple findConfig(int observationWindow, int startingEnv) {
        ConfigResponseTimeTuple returnVal = new ConfigResponseTimeTuple()
        dec normalisation_low = 0.0
        dec normalisation_high = 100.0
        
		//connect to rest API of PAL and set config to index 0 (or any other index you like...)
		RestAPI restAPI = new RestAPI(DEFAULT_SERVER_IP, DEFAULT_SERVER_PORT)
		
		String configs[] = restAPI.getConfigs()
		
		RL learning = new RL()
		learning.setExplorationPenalty(1.0)
		learning.setActions(configs)
		
        int bestConf
        dec bestConfRt = 1000.0
		for (int i = 0; i < 4; i++) {
			//get the learner algorithm's next action, and set that action (composition) in PAL
            int ndx
            if(i == 0) {
			    ndx = startingEnv
            }
            else {
			    ndx = learning.getAction()
            }
			//out.println("choosing action $ndx") // $(configs[ndx].string)")
			restAPI.setConfig(configs[ndx].string)
			
			
			//wait for some time...
			timer.sleep(observationWindow)
			
			//get the perception data from PAL (which is JSON-encoded)
			HTTPResponse r = request.get("http://$DEFAULT_SERVER_IP:$DEFAULT_SERVER_PORT/meta/get_perception", null)
			
			//extract the reward from the JSON data
			JSONElement doc = parser.parseDocument(r.content)
			JSONElement se = null
			
			//NOTE: if your observation window is "too small" you may get zero reward - e.g. for a web server, if no requests were served in that time
			dec reward = 0.0
			
            dec rt = 0.0
			if ((se = parser.getValue(doc, "metrics")) != null) {
				dec vk = iu.intFromString(parser.getValue(se.children[0], "value").value)
				dec vt = iu.intFromString(parser.getValue(se.children[0], "count").value)
                char st[] = parser.getValue(se.children[0], "startTime").value
                char et[] = parser.getValue(se.children[0], "endTime").value
                DateTime start = dateFromStr(st)
                DateTime end = dateFromStr(et)
                DateTime diff = dateUtil.diff(start, end)

                int rt_int = dateUtil.toMilliseconds(diff)
                rt = rt_int / vt
				
				reward = rt
			}
            else {
                out.println("metrics were null mate")
            }
            out.print("offline rt: ")
            out.print(du.makeString(rt))
            out.print("\n")
            if (rt < bestConfRt) {
                bestConf = ndx
                bestConfRt = rt
            }
            out.println("Offline learning round: $(iu.makeString(i))")
            out.print("best conf: ")
            out.print(iu.makeString(bestConf))
            out.print("\n")
            out.print("bestConfRt: ")
            out.print(du.makeString(bestConfRt))
            out.print("\n")
			
			//normalise the reward (which is actually currently "cost", since lower is better)
			dec normalised = scaleCost(reward, normalisation_low, normalisation_high)
			
			//convert what is actually "cost" into reward, where higher is better (ML algorithms assume reward as input, not cost)
			dec finalReward = costToReward(normalised)
			
			//send reward data for this action to the learning algorithm
			learning.setReward(finalReward)
		}
        //keep getting perception from offline server to find a resonable new max rt for this learned config
        dec newMaxRT = 0.0
        for (int i = 0; i < 10; i++) {
			restAPI.setConfig(configs[bestConf].string)
			timer.sleep(observationWindow)
			HTTPResponse r = request.get("http://$DEFAULT_SERVER_IP:$DEFAULT_SERVER_PORT/meta/get_perception", null)
			JSONElement doc = parser.parseDocument(r.content)
			JSONElement se = null
            dec rt = 0.0
			if ((se = parser.getValue(doc, "metrics")) != null) {
				dec vk = iu.intFromString(parser.getValue(se.children[0], "value").value)
				dec vt = iu.intFromString(parser.getValue(se.children[0], "count").value)
                char st[] = parser.getValue(se.children[0], "startTime").value
                char et[] = parser.getValue(se.children[0], "endTime").value
                DateTime start = dateFromStr(st)
                DateTime end = dateFromStr(et)
                DateTime diff = dateUtil.diff(start, end)

                int rt_int = dateUtil.toMilliseconds(diff)
                rt = rt_int / vt
				
                if (rt > newMaxRT) {
                    newMaxRT = rt
                }
			}
            else {
                out.println("find max: metrics were null mate")
            }
        }
        returnVal.config = bestConf
        returnVal.rt = newMaxRT
        learningComplete = 1
        return returnVal
    }

    //dimention 1 = response time
    //dimention 2 = text vol
    //dimention 3 = img vol
    RequestPatternRange extractRequestPatternRanges(Cluster range, int featureCount) {
        
        //extrace the feature ranges from the Cluster
        //feature 1 min
        RequestPatternRange extractedRange = new RequestPatternRange(0.0, 0.0, 0.0, 0.0, 0.0, 0.0)
        if (range == null) {out.print("range")}
        else if (range.members == null) {out.print("members")}
        if (range.members.getLength() > 0) {
            DataPoint x = range.members.getIndex(0)
            extractedRange.imgVolMin = x.point[2]
            extractedRange.imgVolMax = x.point[2]
            extractedRange.textVolMin = x.point[1]
            extractedRange.textVolMax = x.point[1]
            extractedRange.reqCountMin = x.point[0]
            extractedRange.reqCountMax = x.point[0]
            for (int i = 1; i < range.members.getLength(); i++) {
                DataPoint y = range.members.getIndex(i)
                dec values[] = y.point
                if (values[1] > extractedRange.textVolMax) {
                    extractedRange.textVolMax = values[1]
                }
                else if (values[1] < extractedRange.textVolMin) {
                    extractedRange.textVolMin = values[1]
                }
                if (values[2] > extractedRange.imgVolMax) {
                    extractedRange.imgVolMax = values[2]
                }
                else if (values[2] < extractedRange.imgVolMin) {
                    extractedRange.imgVolMin = values[2]
                }
                if (values[0] < extractedRange.reqCountMin) {
                    extractedRange.reqCountMin = values[0]
                }
                else if (values[0] > extractedRange.reqCountMax) {
                    extractedRange.reqCountMax = values[0]
                }
            }
        }

        return extractedRange
    }

    List generateSynthRequestPattern(RequestPatternRange ranges) {
        List synthReqPattern = new List()
        //pick random number in range of observed req count
        int reqCount = 0
        if (ranges.reqCountMin != ranges.reqCountMax) {
            reqCount = ru.getDec(ranges.reqCountMin, ranges.reqCountMax)
        }
        else {
            reqCount = ranges.reqCountMin
        }

        //find ratio of img to text data transmitted from two rnd numbers in text and image ranges
        //split req count by that ratio
        dec imgTransferBytes = 0
        if (ranges.imgVolMin != ranges.imgVolMax) {
            imgTransferBytes = ru.getDec(ranges.imgVolMin, ranges.imgVolMax)
        }
        else {
            imgTransferBytes = ranges.imgVolMin
        }
        dec textTransferBytes = 0
        if (ranges.textVolMin != ranges.textVolMax) {
            textTransferBytes = ru.getDec(ranges.textVolMin, ranges.textVolMax)
        }
        else {
            textTransferBytes = ranges.textVolMin
        }
        dec ratio
        dec imgReqCount
        dec textReqCount
        ratio = textTransferBytes / (imgTransferBytes+textTransferBytes)
        textReqCount = reqCount * ratio
        ratio = imgTransferBytes / (textTransferBytes+imgTransferBytes)
        imgReqCount = reqCount * ratio

        int i_imgReqCount = imgReqCount
        int i_texReqCount = textReqCount

        //find bytes per req
        dec textBytesPerReq = 0
        dec imgBytesPerReq = 0
        if(i_texReqCount != 0) {textBytesPerReq = textTransferBytes / i_texReqCount}
        if(i_imgReqCount != 0) {imgBytesPerReq = imgTransferBytes / i_imgReqCount}

        //find req that is closest to this
        OfflineServerEndpoint textClosest = findClosestSize(serverTextEndpointData, textBytesPerReq)
        OfflineServerEndpoint imgClosest = findClosestSize(serverImageEndpointData, imgBytesPerReq)

        //add req to list for n counts
        for (int i = 0; i < i_texReqCount; i++) {
            synthReqPattern.add(textClosest)
        }
        for (int i = 0; i < i_imgReqCount; i++) {
            synthReqPattern.add(imgClosest)
        }

        return synthReqPattern
    }
    
    //takes a http request file and sends those requests to the offline server
    //designed to be executed asynchornously
    void sendHTTPTraffic(List synthReqPattern, int observationWindow) {
        //run for timerMultiplier number of observationWindows
        while(learningComplete == 0) {
            DateTime start = calendar.getTime()
            int timeDiff = 0
            for (int j = 0; j < synthReqPattern.getLength(); j++) {
                OfflineServerEndpoint x = synthReqPattern.getIndex(j)
                char req[] = new char[]("GET ", "/", x.endpoint.string, " HTTP/1.1\r\n\r\n")

                TCPSocket s = new TCPSocket()
                s.connect(DEFAULT_SERVER_IP, DEFAULT_WEB_PORT)
                s.send(req)
                byte resp[]
                while ((resp = s.recv(RECV_BUF_SIZE)).arrayLength > 0) {
                }
                
                s.disconnect()
            }
                
            DateTime end = calendar.getTime()
            DateTime diff = dateUtil.diff(start, end)
            timeDiff = dateUtil.toMilliseconds(diff) //ms
            //while (timeDiff < observationWindow) {
                //end = calendar.getTime()
                //diff = dateUtil.diff(start, end)
                //timeDiff = dateUtil.toMilliseconds(diff) //ms
            //}
        }
    }

    Policy OfflineConfigLearner:getPolicy(Cluster cluster, int currentOnlineEnv, int observationWindow) {
        //find number of configs for web server
		RestAPI restAPI = new RestAPI(DEFAULT_SERVER_IP, DEFAULT_SERVER_PORT)
		String configs[] = restAPI.getConfigs()
        int numOfConfigs = configs.arrayLength

        List ranges = new List() 

        ranges.add(extractRequestPatternRanges(cluster, 3))
        //query the pal to get persception
            List synthReqPattern = generateSynthRequestPattern(ranges.getIndex(0))
            Thread t = asynch::sendHTTPTraffic(synthReqPattern, observationWindow) 
            ConfigResponseTimeTuple config = findConfig(observationWindow, currentOnlineEnv)            
            Policy policy = new Policy(0, config.config, config.rt)
            t.join()
            learningComplete = 0

        //generate some traffic within classified ranges
        return policy
    }
}

